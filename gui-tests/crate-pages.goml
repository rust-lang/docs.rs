// Checks that the "crate pages" have all the same style (ie: not centered).
go-to: |DOC_PATH| + "/crate/sysinfo"

store-value: (tabs_menu_selector, ".description-container .pure-menu-horizontal ul.pure-menu-list")
store-value: (current_tab, 1)

define-function: (
    "check-tab",
    [tab_name, click_next],
    block {
        // We check the tab's name is the one expected.
        assert-text: (|tabs_menu_selector| + " > li a.pure-menu-active .title", |tab_name|)
        // We check it also matches the one we clicked.
        assert: |tabs_menu_selector| + " > li:nth-of-type(" + |current_tab| + ") a.pure-menu-active"
        // We ensure that the content of the page is not centered.
        assert: "body:not(.centered)"
        assert-css: ("div.container", {"margin": "0px"})
        if: (|click_next|, block {
            store-value: (current_tab, |current_tab| + 1)
            click: |tabs_menu_selector| + " > li:nth-of-type(" + |current_tab| + ")"
        })
    }
)

// If this assert fails, it means we added new entries for this menu so this test needs to be
// updated.
assert-count: (|tabs_menu_selector| + " > li", 4)

call-function: ("check-tab", {"tab_name": " Crate", "click_next": true})
call-function: ("check-tab", {"tab_name": " Source", "click_next": true})
call-function: ("check-tab", {"tab_name": " Builds", "click_next": true})
call-function: ("check-tab", {"tab_name": "Feature flags", "click_next": false})
